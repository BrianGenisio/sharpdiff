using OMetaSharp;

ometa SharpDiff.DiffParser : Parser {
	Header		= "diff" Space FormatType:format FileDefs:files		-> { new Header(format.As<FormatType>(), files.ToIEnumerable<FileDef>()) }
				| "diff" Space FormatType:format					-> { new Header(format.As<FormatType>()) },
	IndexHeader = "index" Space HashRange:range Space Text:mode		-> { new IndexHeader(range.As<HashRange>(), mode.As<string>()) },
	FileDefs	= FileDef+:files									-> { files },
	FileDef		= Space Letter:letter "/" Filename:filename			-> { new FileDef(letter.As<char>(), filename.As<string>()) }
				| Letter:letter "/" Filename:filename				-> { new FileDef(letter.As<char>(), filename.As<string>()) },
	FormatType	= "--" Text:format									-> { new FormatType(format.As<string>()) },
	HashRange	= Text:first ".." Text:second						-> { new HashRange(first.As<string>(), second.As<string>()) },
	Filename	= Text:filename "." Text:extension					-> { filename + "." + extension }
				| Text:filename										-> { filename },
	Text		= LetterOrDigit+:i
}